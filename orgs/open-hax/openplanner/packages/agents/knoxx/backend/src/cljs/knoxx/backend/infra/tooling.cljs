(ns knoxx.backend.infra.tooling
  (:require [clojure.set :as set]
            [clojure.string :as str]
            [knoxx.backend.infra.auth.authz :as authz]
            [knoxx.backend.domain.agent.agent-templates :as templates]
            [knoxx.backend.domain.contracts.resolve :as contract-resolve]
            [knoxx.backend.domain.contracts.roles :as roles]
            [knoxx.backend.infra.http :as backend-http]
            [knoxx.backend.domain.mcp.mcp-bridge :as mcp]
            [knoxx.backend.infra.config :as runtime-config]
            [knoxx.backend.runtime.state :as state]
            [knoxx.backend.infra.registry.tools :as tool-registry]))

(defn- current-config
  []
  (or @state/config*
      (runtime-config/cfg)))

(defn normalize-role
  [role]
  (roles/normalize-role (current-config) role))

(defn- normalize-slugs
  "Normalize role slug strings to kebab-case and deduplicate.
   Collapses mixed forms like 'knowledge_worker' / 'knowledge-worker' into one."
  [slugs]
  (->> (or slugs [])
       (map #(-> % name (str/replace "_" "-")))
       distinct
       vec))

(defn email-enabled?
  [config]
  (and (not (str/blank? (:gmail-app-email config)))
       (not (str/blank? (:gmail-app-password config)))))

(defn discord-enabled?
  [config]
  (not (str/blank? (:discord-bot-token config))))

(defn- auth-tool-policies
  [auth-context]
  (vec (or (:toolPolicies auth-context)
           (:tool-policies auth-context)
           [])))

(defn- policy-effect
  [policy]
  (some-> (:effect policy) str str/lower-case))

(defn- policy-tool-id
  [policy]
  (some-> (or (:toolId policy) (:tool-id policy)) str str/trim not-empty))

(defn auth-tool-ids
  [auth-context]
  (into #{}
        (comp (filter #(= "allow" (policy-effect %)))
              (keep policy-tool-id))
        (auth-tool-policies auth-context)))

(defn- default-contract-tool-policy-clamp?
  [contract-spec auth-context]
  (and auth-context
       (not (authz/system-admin? auth-context))
       (= "knoxx_default" (:id contract-spec))))

(defn- allowed-tool-ids-for
  [contract-spec contract-tool-ids role-tool-ids auth-context]
  (cond
    (and contract-tool-ids (default-contract-tool-policy-clamp? contract-spec auth-context))
    (set/intersection contract-tool-ids (auth-tool-ids auth-context))

    contract-tool-ids contract-tool-ids
    auth-context (auth-tool-ids auth-context)
    :else role-tool-ids))

;; ---------------------------------------------------------------------------
;; Contract resolution (delegated)
;; ---------------------------------------------------------------------------

(defn resolve-actor
  [config actor-id]
  (contract-resolve/resolve-actor config actor-id))

(defn actor-catalog
  [config]
  (contract-resolve/actor-catalog config))

(defn default-actor-id
  [config]
  (contract-resolve/default-actor-id config))

(defn resolve-agent-contract
  ([config contract-id]
   (contract-resolve/resolve-agent-contract config contract-id nil))
  ([config contract-id actor-id]
   (contract-resolve/resolve-agent-contract config contract-id actor-id)))

(defn agent-contract-catalog
  ([config]
   (agent-contract-catalog config nil))
  ([config actor-id]
   (contract-resolve/agent-contract-catalog config actor-id)))

(defn default-agent-contract-id
  ([config]
   (default-agent-contract-id config nil))
  ([config actor-id]
   (contract-resolve/default-agent-contract-id config actor-id)))

(defn effective-agent-contract
  ([config requested-contract-id]
   (effective-agent-contract config requested-contract-id nil))
  ([config requested-contract-id actor-id]
   (contract-resolve/effective-agent-contract config requested-contract-id actor-id)))

;; ---------------------------------------------------------------------------
;; Enforcement + tool catalog
;; ---------------------------------------------------------------------------

(defn ensure-role-can-use!
  ([role tool-id]
   (ensure-role-can-use! nil role tool-id nil nil))
  ([auth-context role tool-id]
   (ensure-role-can-use! auth-context role tool-id nil nil))
  ([auth-context role tool-id agent-contract-id]
   (ensure-role-can-use! auth-context role tool-id agent-contract-id nil))
  ([auth-context role tool-id agent-contract-id actor-id]
   (let [config (current-config)
         contract-spec (effective-agent-contract config agent-contract-id actor-id)
         normalized (roles/normalize-role config (or (:role contract-spec) role))
         contract-tool-ids (some-> contract-spec :tool-ids set)
         role-tool-ids (set (roles/role-tool-ids config normalized))
         allowed (allowed-tool-ids-for contract-spec contract-tool-ids role-tool-ids auth-context)]
     (when-not (contains? allowed tool-id)
       (if auth-context
         (throw (backend-http/http-error 403 "tool_denied" (str "Current Knoxx policy does not allow tool '" tool-id "'")))
         (throw (js/Error. (str "Role '" normalized "' cannot use tool '" tool-id "'")))))
     normalized)))

(defn- resolve-tool-context
  [config role auth-context agent-contract-id actor-id]
  (let [contract-spec (effective-agent-contract config agent-contract-id actor-id)
        actor-spec (or (when actor-id (resolve-actor config actor-id))
                       (when-let [resolved-actor-id (:actor-id contract-spec)]
                         (resolve-actor config resolved-actor-id)))
        normalized (roles/normalize-role config (or (:role contract-spec) role))
        role-tool-ids (set (roles/role-tool-ids config normalized))
        _ (js/console.log "[tooling/resolve-tool-context]"
                                   (clj->js {:contract-id agent-contract-id
                                             :actor-id actor-id
                                             :contract-spec-id (:id contract-spec)
                                             :tool-ids-from-contract (vec (:tool-ids contract-spec))
                                             :role-slugs-from-contract (normalize-slugs (:role-slugs contract-spec))
                                             :actor-role-slugs (normalize-slugs (:role-slugs actor-spec))}))
        contract-tool-ids (some-> contract-spec :tool-ids set)
        allowed-tool-ids (allowed-tool-ids-for contract-spec contract-tool-ids role-tool-ids auth-context)]
    {:contract-spec contract-spec
     :actor-spec actor-spec
     :normalized-role normalized
     :allowed-tool-ids allowed-tool-ids}))

(defn allowed-tool-id-set
  ([config role]
   (allowed-tool-id-set config role nil nil nil))
  ([config role auth-context]
   (allowed-tool-id-set config role auth-context nil nil))
  ([config role auth-context agent-contract-id]
   (allowed-tool-id-set config role auth-context agent-contract-id nil))
  ([config role auth-context agent-contract-id actor-id]
   (:allowed-tool-ids (resolve-tool-context config role auth-context agent-contract-id actor-id))))

(defn- catalog-prompt
  [contract-spec auth-context prompt-key]
  (templates/prompt-value
   (templates/render-prompt (get contract-spec prompt-key) contract-spec auth-context nil)))

(defn- auth-capability-ids
  [config auth-context]
  (->> (authz/ctx-role-slugs auth-context)
       (mapcat #(roles/role-capability-ids config %))
       distinct
       vec))

(defn- catalog-capability-ids
  [config auth-context contract-spec]
  (if (and auth-context (not (authz/system-admin? auth-context)))
    (auth-capability-ids config auth-context)
    (vec (or (:capability-ids contract-spec) []))))

(defn tool-catalog
  ([config role]
   (tool-catalog config role nil nil nil))
  ([config role auth-context]
   (tool-catalog config role auth-context nil nil))
  ([config role auth-context agent-contract-id]
   (tool-catalog config role auth-context agent-contract-id nil))
  ([config role auth-context agent-contract-id actor-id]
   (let [email? (email-enabled? config)
         discord? (discord-enabled? config)
         live-mcp-tool-ids (if (and (:mcp-enabled config) (mcp/available?) (mcp/enabled?))
                             (into #{} (map :id) (mcp/catalog))
                             #{})
         {:keys [contract-spec actor-spec normalized-role allowed-tool-ids]}
         (resolve-tool-context config role auth-context agent-contract-id actor-id)
         base-tools (->> allowed-tool-ids
                         sort
                         (mapv (fn [tool-id]
                                 (let [{:keys [label description]} (tool-registry/get-tool tool-id)]
                                   {:id tool-id
                                    :label label
                                    :description description
                                    :enabled (cond
                                               (= tool-id "email.send") email?
                                               (str/starts-with? tool-id "discord.") discord?
                                               (str/starts-with? tool-id "mcp.") (contains? live-mcp-tool-ids tool-id)
                                               :else true)}))))
         tools (cond-> base-tools
                 (contains? allowed-tool-ids "semantic_query")
                 (conj {:id "graph_query"
                        :label "Graph Query"
                        :description "Query the canonical knowledge graph across workspace/web/bluesky/knoxx-session lakes"
                        :enabled true}))]
     {:role (if auth-context
              (or (:role contract-spec) (authz/primary-context-role auth-context))
              normalized-role)
      :actor_id (:id actor-spec)
      :agent_id (:id contract-spec)
      :agent_label (:id contract-spec)
      :agent_trigger_kind (:trigger-kind contract-spec)
      :role_slugs (normalize-slugs (:role-slugs contract-spec))
      :capability_ids (catalog-capability-ids config auth-context contract-spec)
      :system_prompt (catalog-prompt contract-spec auth-context :system-prompt)
      :actor_system_prompt (catalog-prompt contract-spec auth-context :actor-system-prompt)
      :agent_system_prompt (catalog-prompt contract-spec auth-context :agent-system-prompt)
      :task_prompt (catalog-prompt contract-spec auth-context :task-prompt)
      :email_enabled email?
      :tools tools})))

(defn create-runtime-tools
  "Return eta-mu built-in tool names enabled for this runtime.

   eta-mu 0.70 changed createAgentSession :tools from cwd-bound Tool objects to
   a string allowlist. Passing createReadTool/createBashTool objects now causes
   tool registration/selection failures, so Knoxx must pass names and let eta-mu
   bind built-ins to :cwd itself."
  ([runtime config auth-context]
   (create-runtime-tools runtime config auth-context nil nil nil))
  ([_runtime config auth-context role agent-contract-id actor-id]
   (let [allowed-tool-ids (allowed-tool-id-set config role auth-context agent-contract-id actor-id)
         allowed? (fn [tool-id]
                    (contains? allowed-tool-ids tool-id))]
     (vec
      (remove nil?
              [(when (allowed? "read") "read")
               (when (allowed? "write") "write")
               (when (allowed? "edit") "edit")
               (when (allowed? "bash") "bash")])))))
